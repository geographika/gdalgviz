start: pipeline

?pipeline: step ("!" step)*

step: command step_item*

?step_item: arg | nested

command: CMD_NAME+

arg: short_arg
   | long_arg
   | positional

short_arg: SHORT_FLAG value?
long_arg:  LONG_FLAG ("=" value | value)?
positional: value

value: QUOTED_STRING | BARE_VALUE

nested: "[" pipeline "]"

SHORT_FLAG:    /-[a-zA-Z]/
LONG_FLAG:     /--[a-zA-Z][a-zA-Z0-9\-]*/

// Command words: letters and hyphens only, no dots, no leading digits
// This prevents filenames like n43.tif or input.tif being eaten by command
CMD_NAME:      /[a-zA-Z][a-zA-Z0-9\-]*/

// Bare values: anything not a flag, bracket, !, whitespace, or quote
// Explicitly allows dots, digits-first, equals signs (for KEY=VALUE pairs)
BARE_VALUE.2:  /(?!--?)(?![\[\]!\s"'])[^\[\]!\s"']+/

QUOTED_STRING.3: /"(?:[^"\\]|\\.)*"/ | /'(?:[^'\\]|\\.)*'/

%import common.WS
%ignore WS